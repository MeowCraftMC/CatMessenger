plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'com.rikonardo.papermake' version '1.0.6'
    id 'xyz.jpenilla.run-velocity' version '2.2.2'
    id 'xyz.jpenilla.run-waterfall' version '2.2.2'
}

group = 'cx.rain.mc.catmessenger'
version = '2.1.0'

allprojects {
    repositories {
        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

        maven {
            name = 'Sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }

        maven {
            name = 'SpigotMC'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }

        maven {
            name = 'Sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }

        mavenCentral()
    }
}

subprojects {
    def targetJavaVersion = 17
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release = targetJavaVersion
        }
    }
}

dependencies {
    implementation project(':catmessenger-bungeecord')
    implementation project(':catmessenger-bukkit')
    implementation project(':catmessenger-velocity')
}

jar.finalizedBy(shadowJar)

shadowJar {
    dependencies {
        exclude dependency('org.slf4j:slf4j-api')
        exclude dependency('com.google.code.gson:gson')
    }

    relocate 'com.rabbitmq', 'cx.rain.mc.catmessenger.libs.com.rabbitmq'
}

runVelocity {
    velocityVersion '3.3.0-SNAPSHOT'
}

runWaterfall {
    waterfallVersion '1.20'
}
