plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'com.rikonardo.papermake' version '1.0.6'
}

group = 'cx.rain.mc.catmessenger'
version = '1.0.0'

allprojects {
    repositories {
        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

        maven {
            name = 'Sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }

        maven {
            name = "SpigotMC"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }

        mavenCentral()
    }
}

subprojects {
    def targetJavaVersion = 17
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release = targetJavaVersion
        }
    }
}

dependencies {
    implementation project(':Common')

    // Todo: qyl27: Remove BungeeCord support now.
//    implementation project(':Bungeecord')

    implementation project(':Bukkit')
}

jar.finalizedBy(shadowJar)

shadowJar {
    relocate 'com.google.gson', 'libs.com.google.gson'
    relocate 'com.pengrad', 'libs.com.pengrad'
    relocate 'org.intellij.lang.annotations', 'libs.org.intellij.lang.annotations'
    relocate 'org.jetbrains.annotations', 'libs.org.jetbrains.annotations'
    relocate 'kotlin', 'libs.kotlin'
    relocate 'okhttp3', 'libs.okhttp3'
    relocate 'okio', 'libs.okio'
    relocate 'org.slf4j', 'libs.org.slf4j'
    relocate 'org.java_websocket', 'libs.org.java_websocket'
    relocate 'com.fasterxml.jackson', 'libs.com.fasterxml.jackson'
}
