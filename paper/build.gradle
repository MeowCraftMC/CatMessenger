plugins {
    id 'com.gradleup.shadow'
    id 'xyz.jpenilla.run-paper'
}

version = rootProject.version

dependencies {
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: paper_api_version

    implementation project(':common')

    shadow(project(':common')) {
        transitive false
    }
    shadow group:'io.vertx', name: 'vertx-core', version : vertx_version
    shadow group:'io.vertx', name: 'vertx-rabbitmq-client', version : vertx_version
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

runServer {
    minecraftVersion paper_server_runtime_version
}

shadowJar {
    configurations = [project.configurations.shadow]

    dependencies {
        exclude(dependency('org.slf4j:'))
        exclude(dependency('io.netty:'))
    }

    relocate 'io.vertx', 'catmessenger.libs.vertx'
    relocate 'com.rabbitmq', 'catmessenger.libs.rabbitmq'
    relocate 'com', 'catmessenger.libs.com'
}

jar.finalizedBy(shadowJar)
